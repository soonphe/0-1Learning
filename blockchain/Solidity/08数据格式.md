
### Solidity – 数据位置(data location)
殊的数组，所以也是引用类型)
struct (结构体)
map (映射)
这些类型涉及到的数据量较大，复制它们可能要消耗大量Gas，非常昂贵，所以使用它们时，必须考虑存储位置，例如，是保存在内存中，还是在EVM存储区中。

数据位置(data location)
在合约中声明和使用的变量都有一个数据位置，指明变量值应该存储在哪里。合约变量的数据位置将会影响Gas消耗量。

Solidity 提供4种类型的数据位置。

Storage
Memory
Calldata
Stack
Storage
该存储位置存储永久数据，这意味着该数据可以被合约中的所有函数访问。可以把它视为计算机的硬盘数据，所有数据都永久存储。

保存在存储区(Storage)中的变量，以智能合约的状态存储，并且在函数调用之间保持持久性。与其他数据位置相比，存储区数据位置的成本较高。

Memory
内存位置是临时数据，比存储位置便宜。它只能在函数中访问。

通常，内存数据用于保存临时变量，以便在函数执行期间进行计算。一旦函数执行完毕，它的内容就会被丢弃。你可以把它想象成每个单独函数的内存(RAM)。

Calldata
Calldata是不可修改的非持久性数据位置，所有传递给函数的值，都存储在这里。此外，Calldata是外部函数的参数(而不是返回参数)的默认位置。

Stack
堆栈是由EVM (Ethereum虚拟机)维护的非持久性数据。EVM使用堆栈数据位置在执行期间加载变量。堆栈位置最多有1024个级别的限制。

可以看到，要永久性存储，可以保存在存储区(Storage)。

### Solidity – 变量的数据位置规则
规则1 – 状态变量
状态变量总是存储在存储区中。

pragma solidity ^0.5.0;

contract DataLocation {

// storage     
uint stateVariable;  
uint[] stateArray;  
}  
复制
此外，不能显式地标记状态变量的位置。

pragma solidity ^0.5.0;

contract DataLocation {

uint storage stateVariable; // 错误  
uint[] memory stateArray; // 错误  
}
复制
规则2 – 函数参数与返回值
函数参数包括返回参数都存储在内存中。

pragma solidity ^0.5.0;

contract DataLocation {

// storage     
uint stateVariable;  
uint[] stateArray;

function calculate(uint num1, uint num2) public pure returns (uint result) {
return num1 + num2
}
}
复制
此处，函数参数 uint num1 与 uint num2，返回值 uint result 都存储在内存中。

规则3 – 局部变量
值类型的局部变量存储在内存中。但是，对于引用类型，需要显式地指定数据位置。

pragma solidity ^0.5.0;

contract Locations {

/* 此处都是状态变量 */

// 存储在storage中  
bool flag;  
uint number;  
address account;

function doSomething() public  {

    /* 此处都是局部变量  */  

    // 值类型
    // 所以它们被存储在内存中
    bool flag2;  
    uint number2;  
    address account2;  

    // 引用类型，需要显示指定数据位置，此处指定为内存
    uint[] memory localArray;        
}  
}

复制
不能显式覆盖具有值类型的局部变量。

function doSomething() public  {

    /* 此处都是局部变量  */  

    // 值类型
    bool memory flag2;  // 错误
    uint Storage number2;  // 错误 
    address account2;  

}

复制
规则4 – 外部函数的参数
外部函数的参数(不包括返回参数)存储在Calldata中。

### Solidity – 赋值的数据位置规则
数据可以通过两种方式从一个变量复制到另一个变量。一种方法是复制整个数据(按值复制)，另一种方法是引用复制。

从一个位置复制数据到另一个位置有一定的默认规则。

规则1 – 存储变量赋值给存储变量
将一个状态(存储)变量赋值给另一个状态(存储)变量，将创建一个新的副本。

pragma solidity ^0.5.0;

contract Locations {

uint public stateVar1 = 10;  
uint stateVar2 = 20;

function doSomething() public returns (uint) {

stateVar1 = stateVar2;  
stateVar2 = 30;

return stateVar1; //returns 20  
}  
}   
复制
本例中，stateVar1和stateVar2是状态变量。在doSomething函数中，我们将stateVar2的值复制到stateVar1。

stateVar1的值是20，但是为了确定它创建了一个新的副本，我们改变了stateVar2的值。因此，如果它不创建副本，那么stateVar1的值应该是30，创建副本则是20。

这同样适用于引用类型状态变量。

规则2 – 内存变量复制到存储变量
从内存变量复制到存储变量，总是会创建一个新的副本。

pragma solidity ^ 0.5.0;

contract Locations {

    uint stateVar = 10; //storage

    function doSomething() public returns(uint) {

        uint localVar = 20; //memory    
        stateVar = localVar;  
        localVar = 40;

        return stateVar; //returns 20    
    }  
}  
复制
在上面的例子中，我们有一个状态变量和一个局部变量。在函数中，我们把局部变量的值赋给状态变量。

为了检查行为，我们改变了局部变量的值; 返回状态变量的值。这里可以看到，它会返回20，说明从内存变量复制到存储状态变量，会创建一个新的副本。

规则3 – 存储变量复制到内存变量
从存储变量复制到内存变量，将创建一个副本。

pragma solidity ^ 0.5.0;

contract Locations {

    uint stateVar = 10; //storage

    function doSomething() public returns(uint) {

        uint localVar = 20; //memory    

        localVar = stateVar;  
        stateVar = 40;  

        return localVar; //returns 10    
    }  
}
复制
在这里，将状态变量的值赋给了局部变量，并改变了状态变量的值。

为了检查局部变量的值是否已经更改，返回它的值。可以看到，它会返回10，说明从状态存储变量复制到内存变量，将创建一个副本。

规则4 – 内存变量复制到内存变量
对于引用类型的局部变量，从一个内存变量复制到另一个内存变量不会创建副本。

对于值类型的局部变量仍然创建一个新副本。

引用类型
pragma solidity ^ 0.5.0;

contract Locations {

    function doSomething() 
        public pure returns(uint[] memory, uint[] memory) {

        uint[] memory localMemoryArray1 = new uint[](3);  
        localMemoryArray1[0] = 4;  
        localMemoryArray1[1] = 5;  
        localMemoryArray1[2] = 6;

        uint[] memory localMemoryArray2 = localMemoryArray1;  
        localMemoryArray1[0] = 10;

        return (localMemoryArray1, localMemoryArray2); 
       //returns 10,4,6 | 10,4,6    
    }  
}  
复制
在上面的示例中，我们在内存中初始化了一个名为localMemoryArray1的数组变量，并赋值为4、5和6。然后，我们将该变量复制到名为localMemoryArray2的新内存变量中。

然后，我们修改了localMemoryArray1中第一个索引的值，并返回了两个数组。这将得到相同的结果，因为它们都指向相同的位置。

让我们对值类型进行相同的检查。下面的函数将创建一个新的副本并返回20。

值类型
pragma solidity ^ 0.5.0;

contract Locations {

    function doSomething() public pure returns(uint) {

        uint localVar1 = 10; //memory    
        uint localVar2 = 20; //memory

        localVar1 = localVar2;  
        localVar2 = 40;

        return localVar1; //returns 20    
    }  
}

### Solidity – 字符串
Solidity 中，字符串值使用双引号(“)和单引号(‘)包括，字符串类型用string表示。字符串是特殊的数组，是引用类型。

pragma solidity ^0.5.0;

contract SolidityTest {
string data = "test";
}
复制
在上面的例子中，"test"是一个字符串，data是一个字符串变量。

Solidity提供字节与字符串之间的内置转换，可以将字符串赋给byte32类型变量。

pragma solidity ^0.5.0;

contract SolidityTest {
bytes32 data = "test";
}
复制
转义字符
Solidity 字符串类型支持的转义字符，如下表所示：

序号	转义字符
1	\n
开始新的一行
2	\\
反斜杠
3	\’
单引号
4	\”
双引号
5	\b
退格
6	\f
换页
7	\r
回车
8	\t
制表符
9	\v
垂直制表符
10	\xNN
表示十六进制值并插入适当的字节。
11	\uNNNN
表示Unicode值并插入UTF-8序列。
bytes到字符串的转换
可以使用string()构造函数将bytes转换为字符串。

bytes memory bstr = new bytes(10);
string message = string(bstr);   
复制
示例

bytes到字符串的转换。

pragma solidity ^0.5.0;

contract SolidityTest {   
constructor() public{       
}
function getResult() public view returns(string memory){
uint a = 1;
uint b = 2;
uint result = a + b;
return integerToString(result);
}
function integerToString(uint _i) internal pure
returns (string memory) {

      if (_i == 0) {
         return "0";
      }
      uint j = _i;
      uint len;

      while (j != 0) {
         len++;
         j /= 10;
      }
      bytes memory bstr = new bytes(len);
      uint k = len - 1;

      while (_i != 0) {
         bstr[k--] = byte(uint8(48 + _i % 10));
         _i /= 10;
      }
      return string(bstr);
}
}
复制
可以参考Solidity – 第一个程序中的步骤，运行上述程序。

输出

0: string: 3

### Solidity – 数组
数组是一种数据结构，它是存储同类元素的有序集合。

数组中的特定元素由索引访问，索引值从0开始。例如，声明一个数组变量，如numbers，可以使用numbers[0]、numbers[1]和…，数字[99]表示单个变量。

数组可以是固定大小的，也可以是动态长度的。

对于存储(storage)数组，元素类型可以是任意的(可以是其他数组、映射或结构)。对于内存(memory)数组，元素类型不能是映射类型，如果它是一个公共函数的参数，那么元素类型必须是ABI类型。

类型为bytes和字符串的变量是特殊数组。bytes类似于byte[]，但它在calldata中被紧密地打包。字符串等价于bytes，但(目前)不允许长度或索引访问。

因此，相比于byte[]，bytes应该优先使用，因为更便宜。

声明数组
要声明一个固定长度的数组，需要指定元素类型和数量，如下所示

type arrayName [ arraySize ];
复制
这是一维数组。arraySize必须是一个大于零的整数数字，type可以是任何数据类型。例如，声明一个uint类型，长度为10的数组：balance，如下所示：

uint balance[10];
复制
初始化数组
初始化数组，可以使用下面的语句

uint balance[3] = [1, 2, 3];
复制
可以省略数组长度：

uint balance[] = [1, 2, 3];
复制
将创建与前一个示例完全相同的数组。

balance[2] = 5;
复制
上面的语句，将数组中第3个元素赋值为5。

访问数组元素
可以通过索引访问数组元素。例如

uint salary = balance[2];
复制
上面的语句将从数组中取出第三个元素，并将值赋给salary变量。

创建内存数组
可以使用new关键字在内存中创建动态数组。与存储数组相反，不能通过设置.length成员来调整内存动态数组的长度。

pragma solidity ^0.5.0;

contract C {
function f(uint len) {
uint[] memory a = new uint[](7);
bytes memory b = new bytes(len);
// a.length == 7, b.length == len
a[6] = 8;
}
}
复制
数组成员
length
数组有一个length成员来表示元素数量。动态数组可以通过更改.length成员，在存储器(而不是内存)中调整大小。创建后，内存数组的大小是固定的(但是是动态的，长度可以是函数参数)。

push
动态存储数组和bytes(不是字符串)有一个名为push的成员函数，可用于在数组末尾追加一个元素，函数返回新的长度。

pragma solidity ^0.4.0;

contract ArrayContract {
uint[2**20] m_aLotOfIntegers;
bool[2][] m_pairsOfFlags;

    // newpair存储在内存中——这是函数参数的默认存储位置
    function setAllFlagPairs(bool[2][] newPairs) {
        // 对存储数组赋值，替换传入的整个数组
        m_pairsOfFlags = newPairs;
    }

    function setFlagPair(uint index, bool flagA, bool flagB) {
        // 访问不存在的索引将引发异常
        m_pairsOfFlags[index][0] = flagA;
        m_pairsOfFlags[index][1] = flagB;
    }

    function changeFlagArraySize(uint newSize) {
        // 如果newSize更小，则删除的数组元素将被清除
        m_pairsOfFlags.length = newSize;
    }

    function clear() {
        // 完全清除数组
        delete m_pairsOfFlags;
        delete m_aLotOfIntegers;
        // 效果相同
        m_pairsOfFlags.length = 0;
    }

    bytes m_byteData;

    function byteArrays(bytes data) {
        // byte 数组 ("bytes") 存储时没有填充(padding),
        // 但是可以与“uint8[]”相同处理
        m_byteData = data;
        m_byteData.length += 7;
        m_byteData[3] = 8;
        delete m_byteData[2];
    }

    function addFlag(bool[2] flag) returns (uint) {
        return m_pairsOfFlags.push(flag);
    }

    function createMemoryArray(uint size) returns (bytes) {
        // 使用“new”创建动态内存数组:
        uint[2][] memory arrayOfPairs = new uint[2][](size);
        // 创建一个动态byte数组:
        bytes memory b = new bytes(200);
        for (uint i = 0; i < b.length; i++)
            b[i] = byte(i);
        return b;
    }
}

### Solidity – Enum(枚举)
枚举将一个变量的取值限制为几个预定义值中的一个。精确使用枚举类型有助于减少代码中的bug。

示例

考虑一个鲜榨果汁商店的应用程序，将玻璃杯的容量大小限制为：小、中、大。这将确保任何人不能购买其他容量的果汁。

pragma solidity ^0.5.0;

contract test {
enum FreshJuiceSize{ SMALL, MEDIUM, LARGE }
FreshJuiceSize choice;
FreshJuiceSize constant defaultChoice = FreshJuiceSize.MEDIUM;

function setLarge() public {
choice = FreshJuiceSize.LARGE;
}
function getChoice() public view returns (FreshJuiceSize) {
return choice;
}
function getDefaultChoice() public pure returns (uint) {
return uint(defaultChoice);
}
}
复制
可以使用Solidity – 第一个程序中的步骤，运行上述程序。

首先单击 setLarge 按钮将值设置为 LARGE，然后单击 getChoice 获取所选的选项。

输出

uint8: 2
复制
单击 getDefaultChoice 按钮获得默认选项。

输出

uint256: 1

### Solidity – 结构体(struct)
类似于C语言，Solidity 也有结构体(struct)类型，用于表示复合型数据。结构体是引用类型。

例如，一本书的信息：

Title
Author
Subject
Book ID
就可以用结构体来表示。

定义结构体
要定义结构，使用struct关键字。struct关键字定义了一个新的数据类型，包含多个成员。struct语句的格式如下

struct struct_name {
type1 type_name_1;
type2 type_name_2;
type3 type_name_3;
}
复制
示例

struct Book {
string title;
string author;
uint book_id;
}
复制
访问结构体成员
要访问结构的任何成员，使用成员访问操作符(.)。

示例

定义结构体，使用结构体，访问结构体中的成员。

pragma solidity ^0.5.0;

contract test {
struct Book {
string title;
string author;
uint book_id;
}
Book book;

function setBook() public {
book = Book('Learn Java', 'TP', 1);
}
function getBookId() public view returns (uint) {
return book.book_id;
}
}
复制
可以参考Solidity – 第一个程序中的步骤，运行上述程序。

首先单击setBook按钮，然后单击getBookId获取图书id。

输出

uint256: 1

### Solidity – 映射(mapping)类型
与数组和结构体一样，映射也是引用类型。下面是声明映射类型的语法。

mapping(_KeyType => _ValueType)
复制
_KeyType – 可以是任何内置类型，或者bytes和字符串。不允许使用引用类型或复杂对象。
_ValueType – 可以是任何类型。
注意

映射的数据位置(data location)只能是storage，通常用于状态变量。
映射可以标记为public，Solidity 自动为它创建getter。
示例

pragma solidity ^0.5.0;

contract LedgerBalance {
mapping(address => uint) public balances;

function updateBalance(uint newBalance) public {
balances[msg.sender] = newBalance;
}
}
contract Updater {
function updateBalance() public returns (uint) {
LedgerBalance ledgerBalance = new LedgerBalance();
ledgerBalance.updateBalance(10);
return ledgerBalance.balances(address(this));
}
}
复制
可以参考Solidity – 第一个程序中的步骤，运行上述程序。

首先单击updateBalance按钮将值设置为10，然后查看日志输出：

输出

{
"0": "uint256: 10"
}

### Solidity – 类型转换
Solidity允许类型之间进行隐式转换和显式转换。

隐式转换时必须符合一定条件，不能导致信息丢失。例如，uint8可以转换为uint16，但是int8不可以转换为uint256，因为int8可以包含uint256中不允许的负值。

显式转换
可以使用构造函数语法，显式地将数据类型转换为另一种类型。

int8 y = -3;
uint x = uint(y);
//Now x = 0xfffff..fd == 在256bit长度的格式下，-3的补码表示
复制
转换成更小的类型，会丢失高位。

uint32 a = 0x12345678;
uint16 b = uint16(a); // b = 0x5678
复制
转换成更大的类型，将向左侧添加填充位。

uint16 a = 0x1234;
uint32 b = uint32(a); // b = 0x00001234
复制
转换到更小的字节类型，会丢失后面数据。

bytes2 a = 0x1234;
bytes1 b = bytes1(a); // b = 0x12
复制
转换为更大的字节类型时，向右添加填充位。

bytes2 a = 0x1234;
bytes4 b = bytes4(a); // b = 0x12340000
复制
只有当字节类型和int类型大小相同时，才可以进行转换。

bytes2 a = 0x1234;
uint32 b = uint16(a); // b = 0x00001234
uint32 c = uint32(bytes4(a)); // c = 0x12340000
uint8 d = uint8(uint16(a)); // d = 0x34
uint8 e = uint8(bytes1(a)); // e = 0x12
复制
把整数赋值给整型时，不能超出范围，发生截断，否则会报错。

uint8 a = 12; // no error
uint32 b = 1234; // no error
uint16 c = 0x123456; // error, 有截断，变为 0x3456


### Solidity – 以太单位
Solidity 中，以太币的单位可以使用wei、finney、szabo或ether表示。

最小的单位是wei。1e12表示1 x 10^12。

assert(1 wei == 1);
assert(1 szabo == 1e12);
assert(1 finney == 1e15);
assert(1 ether == 1e18);
assert(2 ether == 2000 fenny);
复制
时间单位
与货币单位相似，Solidity中的时间单位如下：

assert(1 seconds == 1);
assert(1 minutes == 60 seconds);
assert(1 hours == 60 minutes);
assert(1 day == 24 hours);
assert(1 week == 7 days);

### Solidity – 特殊变量/全局变量
特殊变量/全局变量，是全局可用的变量，提供关于区块链的信息。下面列出了常用的特殊变量：

名称	返回
blockhash(uint blockNumber) returns (bytes32)	给定区块的哈希值 – 只适用于256最近区块, 不包含当前区块。
block.coinbase (address payable)	当前区块矿工的地址
block.difficulty (uint)	当前区块的难度
block.gaslimit (uint)	当前区块的gaslimit
block.number (uint)	当前区块的number
block.timestamp (uint)	当前区块的时间戳，为unix纪元以来的秒
gasleft() returns (uint256)	剩余 gas
msg.data (bytes calldata)	完成 calldata
msg.sender (address payable)	消息发送者 (当前 caller)
msg.sig (bytes4)	calldata的前四个字节 (function identifier)
msg.value (uint)	当前消息的wei值
now (uint)	当前块的时间戳
tx.gasprice (uint)	交易的gas价格
tx.origin (address payable)	交易的发送方
示例
示例展示如何使用特殊变量msg，该变量在Solidity中用于获取发送者地址。

pragma solidity ^0.5.0;

contract LedgerBalance {
mapping(address => uint) public balances;

function updateBalance(uint newBalance) public {
balances[msg.sender] = newBalance;
}
}
contract Updater {
function updateBalance() public returns (uint) {
LedgerBalance ledgerBalance = new LedgerBalance();
ledgerBalance.updateBalance(10);
return ledgerBalance.balances(address(this));
}
}
复制
可以参考Solidity – 第一个程序中的步骤，运行上述程序。

首先单击updateBalance按钮将值设置为10，然后查看日志输出。

输出

{
"0": "uint256: 10"
}

### Solidity – 编程风格
良好统一的编程风格，有助于提高代码的可读性和可维护性。下面是有关Solidity编程风格的几条建议。

代码布局
缩进 – 使用4个空格代替制表符作为缩进。避免空格与制表符混用。
空2行规则 – 2个合约定义之间空2行。
pragma solidity ^0.5.0;

contract LedgerBalance {
//...
}


contract Updater {
//...
}
复制
空1行规则 – 2个函数之间空1行。在只有声明的情况下，不需要空行。
pragma solidity ^0.5.0;

contract A {
function balance() public pure;
function account() public pure;
}


contract B is A {
function balance() public pure {
// ...
}

    function account() public pure {
        // ...
    }
}
复制
行长度 – 一行不超过79个字符。
换行规则 – 函数声明中左括号不换行，每个参数一行并缩进，右括号换行，并对齐左括号所在行。
function_with_a_long_name(
longArgument1,
longArgument2,
longArgument3
);

variable = function_with_a_long_name(
longArgument1,
longArgument2,
longArgument3
);

event multipleArguments(
address sender,
address recipient,
uint256 publicKey,
uint256 amount,
bytes32[] options
);

MultipleArguments(
sender,
recipient,
publicKey,
amount,
options
);
复制
源码编码 – UTF-8
Import – Import语句应该放在文件的顶部，pragma声明之后。
函数顺序 – 函数应该根据它们的可见性来分组。
pragma solidity ^0.5.0;

contract A {
constructor() public {
// ...
}

    function() external {
        // ...
    }

    // External functions
    // ...

    // External view functions
    // ...

    // External pure functions 
    // ...

    // Public functions
    // ...

    // Internal functions
    // ...

    // Private functions
    // ...
}
复制
避免多余空格 – 避免在圆括号、方括号或大括号后有空格。
控制结构 – 大括号的左括号不换行，右括号换行，与左括号所在行对齐。
pragma solidity ^0.5.0;

contract Coin {
struct Bank {
address owner;
uint balance;
}
}


if (x < 3) {
x += 1;
} else if (x > 7) {
x -= 1;
} else {
x = 5;
}
if (x < 3)
x += 1;
else
x -= 1;
复制
函数声明 – 使用上面的大括号规则。添加可见性标签。可见性标签应该放在自定义修饰符之前。
function kill() public onlyowner {
selfdestruct(owner);
}
复制
映射 – 在声明映射变量时避免多余空格。
mapping(uint => uint) map; // 不是 mapping (uint => uint) map;
mapping(address => bool) registeredAddresses;
mapping(uint => mapping(bool => Data[])) public data;
mapping(uint => mapping(uint => s)) data;
复制
变量声明 – 声明数组变量时避免多余空格。
uint[] x;  // 不是 unit [] x;
复制
字符串声明 – 使用双引号声明字符串，而不是单引号。
str = "foo";
str = "Hamlet says, 'To be or not to be...'";
复制
代码中各部分的顺序
代码中各部分顺序如下：

Pragma 语句
Import 语句
Interface
库
Contract
在Interface、库或Contract中，各部分顺序应为：

Type declaration / 类型声明
State variable / 状态变量
Event / 事件
Function / 函数
命名约定
合约和库应该使用驼峰式命名。例如，SmartContract, Owner等。
合约和库名应该匹配它们的文件名。
如果文件中有多个合约/库，请使用核心合约/库的名称。
Owned.sol

pragma solidity ^0.5.0;

// Owned.sol
contract Owned {
address public owner;

    constructor() public {
      owner = msg.sender;
    }

    modifier onlyOwner {
        //....
    }

    function transferOwnership(address newOwner) public onlyOwner {
        //...
    }
}
复制
Congress.sol

pragma solidity ^0.5.0;

// Congress.sol
import "./Owned.sol";

contract Congress is Owned, TokenRecipient {
//...
}
复制
结构体名称
驼峰式命名，例如: SmartCoin
事件名称
驼峰式命名，例如：AfterTransfer
函数名
驼峰式命名，首字母小写，比如：initiateSupply
局部变量和状态变量
驼峰式命名，首字母小写，比如creatorAddress、supply
常量
大写字母单词用下划线分隔，例如：MAX_BLOCKS
修饰符的名字
驼峰式命名，首字母小写，例如：onlyAfter
枚举的名字
驼峰式命名，例如：TokenGroup


